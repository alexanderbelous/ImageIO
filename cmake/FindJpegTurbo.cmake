if(WIN32)
  set(LIBJPEG_TURBO_STATIC_SUFFIX "-static")
else()
  set(LIBJPEG_TURBO_STATIC_SUFFIX "")
endif()

include(ExternalProject)
ExternalProject_Add(libjpeg-turbo-project
  # libjpeg-turbo is added from the git submodule.
  SOURCE_DIR "${CMAKE_CURRENT_LIST_DIR}/../thirdparty/libjpeg-turbo"
  PREFIX "${CMAKE_CURRENT_BINARY_DIR}/libjpeg-turbo"
  INSTALL_DIR "${CMAKE_CURRENT_BINARY_DIR}/libjpeg-turbo"
  BUILD_BYPRODUCTS "${CMAKE_CURRENT_BINARY_DIR}/libjpeg-turbo/lib/${CMAKE_STATIC_LIBRARY_PREFIX}turbojpeg${LIBJPEG_TURBO_STATIC_SUFFIX}${CMAKE_STATIC_LIBRARY_SUFFIX}"
  CMAKE_CACHE_ARGS
  -DENABLE_SHARED:BOOL=OFF
  -DENABLE_STATIC:BOOL=ON
  -DWITH_ARITH_DEC:BOOL=ON
  -DWITH_ARITH_ENC:BOOL=ON
  -DWITH_JPEG8:BOOL=OFF
  -DWITH_JPEG7:BOOL=OFF
  -DWITH_TURBOJPEG:BOOL=ON
  -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
  -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
  -DCMAKE_INSTALL_LIBDIR:PATH=<INSTALL_DIR>/lib)

ExternalProject_Get_Property(libjpeg-turbo-project install_dir)
set(LIBJPEG_TURBO_INCLUDE_DIRS "${install_dir}/include/")
set(LIBJPEG_TURBO_LIBRARY "${install_dir}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}turbojpeg${LIBJPEG_TURBO_STATIC_SUFFIX}${CMAKE_STATIC_LIBRARY_SUFFIX}")

# Create the directory so changing INTERFACE_INCLUDE_DIRECTORIES doesn't fail
file(MAKE_DIRECTORY ${LIBJPEG_TURBO_INCLUDE_DIRS})

add_library(libjpeg-turbo STATIC IMPORTED)
set_target_properties(libjpeg-turbo PROPERTIES
  IMPORTED_LOCATION ${LIBJPEG_TURBO_LIBRARY}
  INTERFACE_INCLUDE_DIRECTORIES ${LIBJPEG_TURBO_INCLUDE_DIRS})
add_dependencies(libjpeg-turbo libjpeg-turbo-project)
